# Riboflavin pathway - Q2 (Reactions 1 to 5) 

begin parameters
  k1   1e6
  k1r  1e-4
  k2   0.1
end parameters

begin molecule types
  R5P(a)
  RibA(a)

  D2B4P(h)
  RibH(h)

  D8RL(e)
  RibE(e)

  Riboflavin(c)
  RibC(c)

  FMN(c)
  FAD()
end molecule types

begin seed species
  R5P(a)        5e-7
  RibA(a)       2e-7
  D2B4P(h)      5e-7
  RibH(h)       2e-7
  D8RL(e)       5e-7
  RibE(e)       2e-7

  Riboflavin(c) 0
  RibC(c)       2e-7
  FMN(c)        0
  FAD()         0
end seed species

begin reaction rules
  # Reaction 1: R5P --(RibA)--> D2B4P
  R5P(a) + RibA(a) <-> R5P(a!1).RibA(a!1)    k1, k1r
  R5P(a!1).RibA(a!1) -> D2B4P(h) + RibA(a)   k2

  # Reaction 2: D2B4P --(RibH)--> D8RL
  D2B4P(h) + RibH(h) <-> D2B4P(h!1).RibH(h!1)  k1, k1r
  D2B4P(h!1).RibH(h!1) -> D8RL(e) + RibH(h)    k2

  # Reaction 3: D8RL --(RibE)--> Riboflavin
  D8RL(e) + RibE(e) <-> D8RL(e!1).RibE(e!1)      k1, k1r
  D8RL(e!1).RibE(e!1) -> Riboflavin(c) + RibE(e) k2

  # Reaction 4: Riboflavin --(RibC)--> FMN
  Riboflavin(c) + RibC(c) <-> Riboflavin(c!1).RibC(c!1)  k1, k1r
  Riboflavin(c!1).RibC(c!1) -> FMN(c) + RibC(c)          k2

  # Reaction 5: FMN --(RibC)--> FAD
  FMN(c) + RibC(c) <-> FMN(c!1).RibC(c!1)    k1, k1r
  FMN(c!1).RibC(c!1) -> FAD() + RibC(c)      k2
end reaction rules

begin observables
  Molecules  R5P_free         R5P(a)
  Molecules  D2B4P_free       D2B4P(h)
  Molecules  D8RL_free        D8RL(e)

  Molecules  Riboflavin_free  Riboflavin(c)
  Molecules  FMN_free         FMN(c)
  Molecules  FAD_free         FAD()

  Molecules  Complex_R5P_RibA        R5P(a!1).RibA(a!1)
  Molecules  Complex_D2B4P_RibH      D2B4P(h!1).RibH(h!1)
  Molecules  Complex_D8RL_RibE       D8RL(e!1).RibE(e!1)
  Molecules  Complex_Riboflavin_RibC Riboflavin(c!1).RibC(c!1)
  Molecules  Complex_FMN_RibC        FMN(c!1).RibC(c!1)
end observables

generate_network({overwrite=>1})
simulate_ode({t_end=>100,n_steps=>1000,atol=>1e-10,rtol=>1e-8,sparse=>1})
